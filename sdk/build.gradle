/*
 * Copyright (c) 2016 Schibsted Products & Technology AS. Licensed under the terms of the Apache 2.0 license. See LICENSE in the project root.
 */

import static java.lang.System.getenv

apply plugin: 'jacoco'
apply plugin: 'signing'
apply plugin: 'maven'

group = projectGroup
version = getenv('TRAVIS_TAG') ? getenv('TRAVIS_TAG') : "0.0.1";

dependencies {
    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    compile "com.amazonaws:aws-java-sdk-core:$awsVersion"
    compile "com.amazonaws:aws-java-sdk-dynamodb:$awsVersion"
    compile "com.amazonaws:aws-java-sdk-kms:$awsVersion"
    compile "com.amazonaws:aws-java-sdk-iam:$awsVersion"
    compile "com.amazonaws:aws-java-sdk-sts:$awsVersion"
    compile "com.amazonaws:aws-encryption-sdk-java:$awsEncryptionVersion"

    compile "com.google.guava:guava:$guavaVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"

    testCompile "org.hamcrest:hamcrest-all:$hamcrestVersion"
    testCompile "org.hamcrest:hamcrest-core:$hamcrestVersion"
    testCompile ("org.testng:testng:$testngVersion") {
        exclude module : 'junit'
    }
    testCompile "org.mockito:mockito-core:$mockitoVersion"
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output

        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    testLogging {
        events "passed", "skipped", "failed"
    }

    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task jacocoIntegrationTestReport(type: JacocoReport) {
   sourceSets sourceSets.main
   executionData integrationTest
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    sign configurations.archives
}

shadowJar {
    baseName = project.name + '-shadow'
    classifier = ''
}

tasks.withType(Jar) {
    from(project.parent.projectDir) {
        include 'LICENSE', 'NOTICE'
        into 'META-INF'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'strongbox-' + project.name
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            pom.withXml {
                asNode().with {
                    appendNode('name', 'Strongbox SDK')
                    appendNode('description', 'A secret management solution')
                    appendNode('url', 'https://github.com/schibsted/strongbox')
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('name', 'Apache-2.0')
                            appendNode('url', 'https://github.com/schibsted/strongbox/blob/master/LICENSE')
                        }
                    }
                }
            }
        }
        shaded(MavenPublication) {
            artifactId = 'strongbox-' + project.name + '-shaded'
            from components.shadow
        }
    }
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Strongbox SDK'
        packaging 'jar'
        description 'A secret management solution'
        url 'https://github.com/schibsted/strongbox'

        scm {
          connection 'scm:git:git://github.com/schibsted/strongbox.git'
          developerConnection 'scm:git:ssh://github.com:schibsted/strongbox.git'
          url 'https://github.com/schibsted/strongbox'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            name 'Stian Kristoffersen'
            email 'strongbox@schibsted.com'
            organization 'Schibsted'
            organizationUrl 'https://schibsted.com'
          }
        }
      }
    }
  }
}

javadoc {
    exclude("**/internal/**")
}
