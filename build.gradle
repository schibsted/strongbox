/*
 * Copyright (c) 2016 Schibsted Products & Technology AS. Licensed under the terms of the Apache 2.0 license. See LICENSE in the project root.
 */

import static java.lang.System.getenv

buildscript {
    repositories {
        gradlePluginPortal()
    }

    dependencies {
        //Check for the latest version here: https://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "com.netflix.nebula:gradle-lint-plugin:16.26.0"
	classpath "org.owasp:dependency-check-gradle:8.1.0"
    }
}

version = getenv('TRAVIS_TAG') ? getenv('TRAVIS_TAG') : "0.0.1";

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'nebula.lint'
    apply plugin: "org.owasp.dependencycheck"

    repositories {
        mavenCentral()
    }

    archivesBaseName = 'strongbox-' + project.name

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    gradleLint.rules = ['unused-dependency']

    compileJava {
        sourceCompatibility = '1.8'
    }

    ext {
        airlineVersion = '0.8'
        apacheCommonsVersion = '1.2'
        archaiusVersion = '0.7.5'
        awsEncryptionVersion = '2.4.0'
        awsVersion = '1.12.395'
        // can't update guava to latest version until airline is fixed, see https://github.com/airlift/airline/pull/53
        guavaVersion = '31.1-jre'
        hamcrestVersion = '1.3'
        jacksonVersion = '2.14.2'
        jaxbVersion = '2.3.1'
        mockitoVersion = '3.7.0'
        slf4jVersion = '1.7.25'
        springVersion = "4.2.6.RELEASE"
        springBootVersion = "1.3.5.RELEASE"
        springCloudVersion = "1.1.0.RELEASE"
        testngVersion = '6.11'
    }

    jacoco {
        // Broken in newer versions. Need this version for now.
        toolVersion = '0.8.6+'
    }

    dependencyCheck {
      cve {
        startYear = 2021
      }
    }
}

subprojects {
    dependencies {
        implementation("javax.xml.bind:jaxb-api:$jaxbVersion")
    }
    test {
        // enable TestNG support (default is JUnit)
        useTestNG()
    }
}

task install(dependsOn: 'cli:installDist') {
    doLast {
        // If user has overridden the install directory, use the override, else default to the build directory
        // so that it will be cleaned up by the gradlew clean command.
        def installRoot = "$buildDir"
        if (project.hasProperty("installDir")) {
            installRoot = project.installDir
            assert installRoot != ""
        }

        def cliInstallRoot = 'cli/build/install/cli'
        def cliBin = cliInstallRoot + '/bin/'
        def cliLib = cliInstallRoot + '/lib/'

        // The copy won't fail if the directories don't exist, so check that they were actually generated before
        // continuing. It will make debugging problems faster.
        assert file(cliBin).exists()
        assert file(cliLib).exists()
        copy {
            from cliBin
            into installRoot + '/bin'
        }
        copy {
            from cliLib
            into installRoot + '/lib'
        }
        println 'Finished installing to ' + installRoot + '/bin'
    }
}

idea {
    project {
        jdkName = sourceCompatibility
        languageLevel = sourceCompatibility
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}
