/*
 * Copyright (c) 2016 Schibsted Products & Technology AS. Licensed under the terms of the Apache 2.0 license. See LICENSE in the project root.
 */

import org.ajoberstar.grgit.Grgit
import java.time.Instant
import static java.lang.System.getenv

plugins {
    id "org.ajoberstar.grgit" version "1.4.2"
    id 'org.openjfx.javafxplugin' version '0.0.13' apply false
}

if (JavaVersion.current().java11Compatible) {
    apply plugin: 'org.openjfx.javafxplugin'
    javafx {
        version = "11.0.2"
        modules = [ 'javafx.controls' ]
    }
}

apply plugin: 'application'
apply plugin: 'signing'

mainClassName = 'com.schibsted.security.strongbox.cli.StrongboxCLI'
version = getenv('GITHUB_REF') ? getenv('GITHUB_REF').replace("refs/tags/","") : "0.0.1";

dependencies {
    implementation project(':sdk')

    implementation("com.amazonaws:aws-java-sdk-sts:$awsVersion")
    implementation("com.amazonaws:aws-java-sdk-core:$awsVersion")

    implementation("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
    implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion")
    implementation("com.google.guava:guava:$guavaVersion")
    implementation("org.slf4j:slf4j-api:$slf4jVersion")

    implementation("io.airlift:airline:$airlineVersion")

    runtimeOnly("org.slf4j:slf4j-jcl:$slf4jVersion")

    testImplementation("org.hamcrest:hamcrest-all:$hamcrestVersion")
    testImplementation("org.hamcrest:hamcrest-core:$hamcrestVersion")
    testImplementation("org.testng:testng:$testngVersion") {
        exclude module : 'junit'
    }
}

installDist {
    doLast {
        def cliBase = 'build/install/cli/'
        def cliBin = cliBase + 'bin/'
        def projectRoot = project.parent.projectDir.absoluteFile.toString() + '/'

        file(cliBin + 'cli').renameTo(file(cliBin + '/strongbox'))
        file(cliBin + 'cli.bat').renameTo(file(cliBin + 'strongbox.bat'))

        copy {
            from  projectRoot + 'LICENSE', projectRoot + 'NOTICE'
            into cliBase
        }
    }
}

jar {
    manifest {
        attributes("Implementation-Version": this.version)
        Grgit.hashCode();
    }
}

tasks.withType(Jar) {
    from(project.parent.projectDir) {
        include 'LICENSE', 'NOTICE'
        into 'META-INF'
    }
}

task cliTar(type: Tar, dependsOn: "installDist") {
    from(installDist)
    into('strongbox-cli')
    archiveFileName.set("strongbox-cli.tar.gz")
    archiveExtension.set("tar.gz")

    compression = Compression.GZIP
}

signing {
    required { project.hasProperty('signing.password') && project.hasProperty('signing.keyId') && project.hasProperty('signing.secretKeyRingFile') }
    sign cliTar
}
